apply from: "$rootDir/gradle/instrumentation.gradle"

muzzle {
  pass {
    group = "io.ratpack"
    module = 'ratpack-core'
    versions = "[1.4.0,)"
    skipVersions += ["0.9.12", "0.9.13", "0.9.14",]
  }
}

dependencies {
  library group: 'io.ratpack', name: 'ratpack-core', version: '1.4.0'

  implementation project(':instrumentation:netty:netty-4.1:javaagent')

  testLibrary group: 'io.ratpack', name: 'ratpack-groovy-test', version: '1.4.0'

  // Need to force version of Guava older than opentelemetry-sdk's implicit dependency to have one
  // that is compatible with ratpack.
  testImplementation("com.google.guava:guava") {
    version {
      strictly("20.0")
    }
  }

  if (JavaVersion.current().isCompatibleWith(JavaVersion.VERSION_11)) {
    testImplementation group: 'com.sun.activation', name: 'jakarta.activation', version: '1.2.2'
  }
}
