apply plugin: 'io.opentelemetry.javaagent.instrumentation-instrumentation'
apply from: "$rootDir/gradle/java.gradle"

minimumBranchCoverage = 0.5
minimumInstructionCoverage = 0.5
excludedClassesCoverage += [
  'io.opentelemetry.instrumentation.test.asserts.*Assert*',
  'io.opentelemetry.instrumentation.test.base.*',
  'io.opentelemetry.instrumentation.test.log.*',
  'io.opentelemetry.instrumentation.test.AgentTestRunner',
  'io.opentelemetry.instrumentation.test.InstrumentationTestRunner',
  'io.opentelemetry.instrumentation.test.InMemoryExporter.*',
  'io.opentelemetry.instrumentation.test.utils.*',
  // Avoid applying jacoco instrumentation to classes instrumented by tested agent
  'context.ContextTestInstrumentation**',
]

dependencies {
  api(project(path: ':opentelemetry-sdk-shaded-for-testing', configuration: 'shadow'))

  api deps.guava
  api deps.spock

  implementation deps.opentelemetryApi
  implementation deps.bytebuddy
  implementation deps.bytebuddyagent
  implementation deps.slf4j
  implementation deps.testLogging

  api group: 'com.squareup.okhttp3', name: 'okhttp', version: '4.9.0'
  api group: 'com.squareup.okhttp3', name: 'logging-interceptor', version: '4.9.0'

  //TODO replace with Servlet API?
  implementation group: 'org.eclipse.jetty', name: 'jetty-server', version: '8.0.0.v20110901'

  implementation project(':javaagent-bootstrap')
  implementation project(':javaagent-api')
  implementation project(':instrumentation-api')
  implementation(project(':javaagent-tooling')) {
    // including :opentelemetry-sdk-shaded-for-testing above instead
    exclude group: 'io.opentelemetry', module: 'opentelemetry-sdk'
  }
  api project(':utils:test-utils')

  annotationProcessor deps.autoservice
  implementation deps.autoservice

  api deps.groovy

  testImplementation deps.opentelemetryExtAnnotations
  testImplementation project(':instrumentation:external-annotations:javaagent')

  testImplementation group: 'cglib', name: 'cglib', version: '3.2.5'
  // test instrumenting java 1.1 bytecode
  //TODO do we want this?
  testImplementation group: 'net.sf.jt400', name: 'jt400', version: '6.1'

  // We have autoservices defined in test subtree, looks like we need this to be able to properly rebuild this
  testAnnotationProcessor deps.autoservice
  testImplementation deps.autoservice
}

// See comment for FieldBackedProviderFieldInjectionDisabledTest about why this hack is here
tasks.register("testDisabledFieldInjection", Test) {
  systemProperties "otel.javaagent.runtime.context.field.injection": "false"
  includes = ["context/FieldBackedProviderFieldInjectionDisabledTest.class"]
}
test.dependsOn(testDisabledFieldInjection)
test.forkEvery 1

javadoc.enabled = false
