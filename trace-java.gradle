plugins {
  id 'io.franzbecker.gradle-lombok' version '1.14' // Last to support Java 7

  id "com.jfrog.artifactory" version "4.15.2" apply false
  id 'com.jfrog.bintray' version '1.8.5' apply false
  id "nebula.release" version "15.0.1"

  id 'org.unbroken-dome.test-sets' version '2.2.1'
  id 'com.github.ben-manes.versions' version '0.27.0'

  // Not applying google java format by default because it gets confused by stray java build
  // files in 'workspace' build directory in CI
  id 'com.github.sherter.google-java-format' version '0.8' apply false
  id 'com.dorongold.task-tree' version '1.5'

  id "com.github.johnrengelman.shadow" version "5.2.0"

  id "com.diffplug.gradle.spotless" version "3.28.1"
  id "com.github.spotbugs" version "4.0.1"
}

def isCI = System.getenv("CI") != null

allprojects {
  group = 'io.opentelemetry.auto'
  version = '0.5.0-SNAPSHOT'

  if (isCI) {
    buildDir = "$rootDir/workspace/${projectDir.path.replace(rootDir.path, '')}/build/"
  }

  apply from: "$rootDir/gradle/dependencies.gradle"
  apply from: "$rootDir/gradle/util.gradle"
}

repositories {
  mavenLocal()
  jcenter()
  mavenCentral()
}

description = 'trace-java'

wrapper {
  distributionType = Wrapper.DistributionType.ALL
}

allprojects {
  tasks.withType(JavaForkOptions).configureEach {
    maxHeapSize = System.properties["ota.forkedMaxHeapSize"]
    minHeapSize = System.properties["ota.forkedMinHeapSize"]
    jvmArgs "-XX:ErrorFile=/tmp/hs_err_pid%p.log"
  }
}
